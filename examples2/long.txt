record BufferedReader begin
  text: String;
  index: Int;
  line: String;
  finished: Bool
end

union IntList begin
  Nil[];
  Cons[Int, IntList]
end

func listLength(list: IntList): Int begin
  match list begin
    Nil[] -> 0
    Cons[x, rest] -> 1 + listLength(rest)
  end
end

func createList(): Promise(IntList) begin
  promise list, initList: IntList in
  initList <- Nil[];
  list
end

func append(list: Promise(IntList), x: Int): Unit begin
  list <~ Cons[x, ?list]
end

func buffered(text: String): Promise(BufferedReader) begin
  promise reader, initReader: BufferedReader in
  initReader <- BufferedReader {
    text=text,
    index=0,
    line="",
    finished=false
  };
  reader
end

func tryNextLine(reader: BufferedReader): BufferedReader begin
  if reader.finished || reader.index >= length(reader.text) then
    BufferedReader {
      text="",
      index=0,
      line=reader.text,
      finished=true
    }
  else
    if charAt(reader.text, reader.index) == "\n" then
      let before: String = substring(reader.text, 0, reader.index) in
      let after: String = substring(reader.text, reader.index + 1, length(reader.text)) in
      BufferedReader {
        text=after,
        index=0,
        line=before,
        finished=false
      }
    else
      let new_reader: BufferedReader = BufferedReader {
        text=reader.text,
        index=reader.index+1,
        line=reader.line,
        finished=false
      } in
      tryNextLine(new_reader)
    end
  end
end

func isFinished(reader: Promise(BufferedReader)): Bool begin
  (?reader).finished
end

func nextLine(reader: Promise(BufferedReader)): String begin
  reader <~ tryNextLine(?reader);
  (?reader).line
end

func parseDigit(text: String): Int begin
  promise digit, initDigit: Int in
  initDigit <- -1;
  for guess = 0 to 9 begin
    if charAt(text, 0) == intToString(guess) then
      digit <~ guess
    else
      ()
    end
  end;
  ?digit
end

func parseInt(text: String): Int begin
  if length(text) == 0 then
    0
  else
    if charAt(text, 0) == "-" then
      0 - parseInt(substring(text, 1, length(text)))
    else
      promise num, initNum: Int in
      initNum <- 0;
      for i = 0 to length(text) begin
        num <~ ?num * 10;
        let currentDigitText: String = charAt(text, i) in
        let currentDigitValue: Int = parseDigit(currentDigitText) in
        if currentDigitValue == -1 then
          num <~ 0 -- Reset if we bumped into a parse error
        else
          num <~ ?num + currentDigitValue
        end
      end;
      ?num
    end
  end
end

func readAsync(file: String): Promise(String) begin
  promise getText, setText: String in
  async setText <- readFile(file);
  getText
end

func printLines(file: String): Unit begin
  let reader = buffered(?readAsync(file)) in
  while !isFinished(reader) begin
    let line = nextLine(reader) in
    println(line)
  end
end

func countLines(text: String): Int begin
  promise count, initCount: Int in
  initCount <- 0;
  for i = 0 to length(text) begin
    if charAt(text, i) == "\n" then
      count <~ ?count + 1
    else
      ()
    end
  end;
  ?count
end

func pow(num: Int, exp: Int): Int begin
  if exp < 0 then
    0
  else
    if exp == 0 then
      1
    else
      if exp == 1 then
        num
      else
        num * pow(num, exp - 1)
      end
    end
  end
end

func square(num: Int): Int begin
  pow(num, 2)
end

func cube(num: Int): Int begin
  pow(num, 3)
end

func max(a: Int, b: Int): Int begin
  if a >= b then
    a
  else
    b
  end
end

func min(a: Int, b: Int): Int begin
  if a <= b then
    a
  else
    b
  end
end

func sumLinesSquaredOrCubed(file: String): Int begin
  promise sum, initSum: Int in
  initSum <- 0;
  let text = ?readAsync(file) in
  let numLines = countLines(text) in
  let reader = buffered(text) in
  for i = 0 to numLines begin
    let line = nextLine(reader) in
    let number = parseInt(line) in
    let squared = square(number) in
    let cubed = cube(number) in
    let larger = max(squared, cubed) in
    sum <~ ?sum + larger
  end;
  ?sum
end

func buildList(file: String): IntList begin
  let reader = buffered(readFile(file)) in
  let list = createList() in
  while !isFinished(reader) begin
    let line = nextLine(reader) in
    let num = parseInt(line) in
    append(list, num)
  end;
  ?list
end

func listMax(list: IntList): Int begin
  match list begin
    Nil[] -> 0 -- Should really be intMin to handle negatives correctly...
    Cons[x, rest] ->
      max(x, listMax(rest))
  end
end

func main(): Unit begin
  printLines("Long.java");
  for i = min(square(3), cube(2)) to max(square(3), cube(2)) begin
    println(concat("On iteration: ", intToString(i)))
  end;
  let sum = sumLinesSquaredOrCubed("numbers.txt") in
  println(intToString(min(0, sum)));
  let intList = buildList("numbers.txt") in
  let maxNumber = listMax(intList) in
  let announcement = concat("Maximum number was ", intToString(maxNumber)) in
  println(announcement)
end
